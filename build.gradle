plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'edu.wpi.sga'
version = '3.0.0'
sourceCompatibility = '11'

// You'll need to create an integration-test folder with the same structure as your test or main folders
sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime

	compileOnly {
		extendsFrom annotationProcessor
	}
}

// This will be the name of the task that you'll run to do only your integration tests
task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
}

repositories {
	mavenCentral()
}

ext {
	set('azureVersion', "3.12.1")
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.3'
	implementation 'org.springframework.boot:spring-boot-starter-web:2.6.3'
	implementation 'com.azure.spring:azure-spring-boot-starter-active-directory'
	implementation 'org.flywaydb:flyway-core:8.4.1'
	implementation 'org.flywaydb:flyway-mysql:8.4.1'
	implementation 'org.mapstruct:mapstruct:1.4.2.Final'
	implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	compileOnly 'org.projectlombok:lombok:1.18.22'
	runtimeOnly 'mysql:mysql-connector-java:8.0.27'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
	annotationProcessor 'org.hibernate:hibernate-jpamodelgen:5.6.5.Final'

	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.3'

	integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.3'
}

dependencyManagement {
	imports {
		mavenBom "com.azure.spring:azure-spring-boot-bom:${azureVersion}"
	}
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

test {
	useJUnitPlatform()
}

integrationTest {
	useJUnitPlatform()
}